plugins {
	id 'java-library'
	id 'maven-publish'
	id 'signing'
}

sourceCompatibility = 1.11
def baseVersion = '3.9.4'
def baseGroupId = 'io.github.adempiere'

repositories {
    mavenLocal()
    mavenCentral()
}

allprojects {
    tasks.withType(Javadoc).all { enabled = false }
}

sourceCompatibility = 1.11
targetCompatibility = 1.11

dependencies {
   api fileTree(
        dir: 'lib',
        include: [
            '*.jar'
        ]
    )
    //	ADempiere Core
    api "${baseGroupId}:human-resource-and-payroll:${baseVersion}"
	api "${baseGroupId}:store:${baseVersion}"
	// https://search.maven.org/artifact/javax.mail/javax.mail-api/1.6.2/jar
	api 'javax.mail:javax.mail-api:1.6.2'
	// http://xml.apache.org/commons/#external
	api 'xml-apis:xml-apis:2.0.2'
	api "${baseGroupId}:withholding-engine:adempiere-3.9.4-1.3.2"
	api 'io.vavr:vavr:0.10.4'
}

sourceSets {
    main {
         java {
            srcDirs = ['core/src/main/java/base', 'financial/src/main/java/base', 'withholding/src/main/java/base', 'human_resource/src/main/java/base']
         }
    }
}

java {
    withJavadocJar()
    withSourcesJar()
}


def entityType = 'LVE'
group = "io.github.adempiere"
version = findProperty("deployVersion") ?: "local-1.0.0"

jar {
    manifest {
        attributes("Implementation-Title": "Venezuela Location for ADempiere",
                   "Implementation-Version": version, 
                   "EntityType": entityType)
    }
}

publishing {
    repositories {
		mavenLocal()

        maven {
            url = findProperty("deployPublishUrl") ?: System.properties['deploy.publish_url']
            credentials {
                username = findProperty("deployUsername") ?: System.properties['deploy.user']
                password = findProperty("deployPassword") ?: System.properties['deploy.token']
            }
        }
    }
    publications {
        mavenJava(MavenPublication) {
			groupId = group
			artifactId = 'location-venezuela'
			version = version
           	from components.java
           	pom {
                name = 'ADempiere Venezuela Location'
                description = 'This library have all related to ADempiere Venezuela Location own Laws for Venezuela'
                url = 'http://erpya.com/'
                licenses {
                    license {
                        name = 'GNU General Public License v3.0'
                        url = 'https://www.gnu.org/licenses/gpl-3.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'yamelsenih'
                        name = 'Yamel Senih'
                        email = 'ysenih@erpya.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/adempiere/lve.git'
                    developerConnection = 'scm:git:ssh://github.com/adempiere/lve.git'
                    url = 'http://github.com/adempiere/lve'
                }
            }
		}
	}
}


task cleanBuildPublishLocal(type: GradleBuild) {
	tasks = ['clean', 'build', 'publishToMavenLocal']
}


signing {
	def isReleaseVersion = !version.toString().startsWith("local") && !version.toString().endsWith("-SNAPSHOT")

	sign configurations.archives

	setRequired {
		// signing is required if this is a release version and the artifacts are to be published
		// do not use hasTask() as this require realization of the tasks that maybe are not necessary
		(isReleaseVersion || version.toString().equals("build")) && gradle.taskGraph.allTasks.any {
			it.equals(PublishToMavenRepository)
		}
	}
	def signingKey = findProperty("deploySigningKey")
    def signingPassword = findProperty("deploySigningPassword")
    useInMemoryPgpKeys(signingKey, signingPassword)
    sign publishing.publications.mavenJava
}
